plugins {
	id 'org.springframework.boot' version '2.6.7'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
	id 'org.flywaydb.flyway' version "8.5.10"
	id "nu.studer.jooq" version "7.1.1"
}

group = 'com.tokoroga-dokkoi.java'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-jooq'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
	implementation 'org.springframework.security:spring-security-oauth2-jose'
	implementation 'org.flywaydb:flyway-core'
	// https://mvnrepository.com/artifact/com.github.f4b6a3/ulid-creator
	implementation 'com.github.f4b6a3:ulid-creator:4.1.2'
	implementation 'commons-validator:commons-validator:1.7'
	runtimeOnly 'org.postgresql:postgresql'
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testCompileOnly 'org.projectlombok:lombok'
	testAnnotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	jooqGenerator 'org.postgresql:postgresql'
	jooqGenerator "jakarta.xml.bind:jakarta.xml.bind-api:3.0.1"
	testImplementation "org.mockito:mockito-core:4.5.1"
	testImplementation "net.bytebuddy:byte-buddy:1.12.9"

	implementation 'com.auth0:auth0:1.41.0'
}

tasks.named('test') {
	useJUnitPlatform()
}

flyway {
	url = System.getenv("DB_URL") ?: "jdbc:postgresql://localhost:5432/realworldapp-local"
	user = System.getenv("DB_USER") ?: "root"
	password = System.getenv("DB_PASSWORD") ?: "passWord"
	baselineVersion = "1.0.0"
	baselineOnMigrate = true
	locations = ["filesystem:src/main/resources/db/migration"]
}

jooq {
	configurations {
		main {
			generationTool {
				jdbc {
					driver = "org.postgresql.Driver"
					url = System.getenv("DB_URL") ?: "jdbc:postgresql://localhost:5432/realworldapp-local"
					user = System.getenv("DB_USER") ?: "root"
					password = System.getenv("DB_PASSWORD") ?: "passWord"
				}
				generator {
					name = "org.jooq.codegen.DefaultGenerator"
					target {
						packageName = "com.tokorogadokkoi.java.realwordapp"
						directory = "build/generated-src/jooq/main"
					}
					database {
						name = "org.jooq.meta.postgres.PostgresDatabase"
						inputSchema = "public"
					}
				}
			}
		}
	}
}